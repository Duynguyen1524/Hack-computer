/**
 * 16-bit Hack ALU from the book "The Elements of Computing Systems"
 * by Nisan and Schocken, MIT Press.
 * 
 * The ALU computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * 
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 * 
 * Implementation: the ALU logic manipulates the x and y inputs
 * and operates on the resulting values, as follows:
 * if (zx == 1) set x = 0        // 16-bit constant
 * if (nx == 1) set x = !x       // bitwise not
 * if (zy == 1) set y = 0        // 16-bit constant
 * if (ny == 1) set y = !y       // bitwise not
 * if (f == 1)  set out = x + y  // integer 2's complement addition
 * if (f == 0)  set out = x & y  // bitwise and
 * if (no == 1) set out = !out   // bitwise not
 * if (out == 0) set zr = 1
 * if (out < 0) set ng = 1
 * 
 * Author: Naga Kandasamy
 * Date created: July 10, 2020
 * Date modified: October 4, 2024
 * 
 * Name of student(s): Jean A. Garcia, Justin Nguyen
 * Date modified: 
*/

CHIP ALU {
    IN  
        x[16], y[16],       // 16-bit inputs        
        zx,                 // zero the x input?
        nx,                 // negate the x input?
        zy,                 // zero the y input?
        ny,                 // negate the y input?
        f,                  // compute out = x + y (if 1) or x & y (if 0)
        no;                 // negate the out output?

    OUT 
        out[16],            // 16-bit output
        zr,                 // 1 if (out == 0), 0 otherwise
        ng;                 // 1 if (out < 0),  0 otherwise

    PARTS:
    // TODO: complete the implementation of the ALU
    Mux16(a = x, b = false, sel = zx, out = out1);
    Not16(in = out1, out = out1p);
    Mux16(a = out1, b = out1p, sel = nx, out = out2);

    Mux16(a = y, b = false, sel = zy, out = out3);
    Not16(in = out3, out = out3p );
    Mux16(a = out3, b = out3p, sel = ny, out = out4);


    And16(a = out2, b = out4, out = out5);
    Add16(a = out2, b = out4, out = out6);
    Mux16(a = out5, b= out6, sel = f, out = postout);

    Not16( in = postout, out = postoutp);
    Mux16( a = postout, b = postoutp, sel = no, out = out ,  out[0..7] = lsb, out[8..15] = msb, out[15]= l);

    Or8Way(in=lsb, out=orLower);
    Or8Way(in=msb, out=orUpper);
    Or(a=orLower, b=orUpper, out=zrNot);
    Not(in=zrNot, out=zr);


    Or(a= l,b = false, out = ng );
}